{
  pkgs,
  lib,
  extraPackages,
  ...
}:
let
  # Recursively get all dependencies of packages added
  getDeps =
    p: [ p ] ++ lib.flatten (map getDeps (p.propagatedBuildInputs or [ ]));
  packages = lib.unique (lib.flatten (map getDeps extraPackages));
  rEnv = pkgs.rWrapper.override { inherit packages; };
in
pkgs.stdenv.mkDerivation rec {
  pname = "rstudio";
  version = "2025.09.2-418";

  src = pkgs.fetchurl {
    url = "https://download1.rstudio.org/electron/jammy/amd64/rstudio-${version}-amd64.deb";
    sha256 = "Hspso1erlNyDRK4eUyYlLLakC4Kcax7MhoXPs6eKaGk=";
  };

  nativeBuildInputs = with pkgs; [
    dpkg
    autoPatchelfHook
    makeWrapper
  ];

  buildInputs = with pkgs; [
    glib
    gtk3
    libglvnd
    # Added
    mesa
    openssl
    R
    rEnv # Reference to packages
    # to here
    stdenv.cc.cc.lib
    xorg.libX11
    xorg.libxcb
    xorg.libXcomposite
    xorg.libXdamage
    xorg.libXext
    xorg.libXfixes
    xorg.libXrandr
    alsa-lib
    cups
    dbus
    expat
    nspr
    nss
    fontconfig
    freetype
  ];

  autoPatchelfIgnoreMissingDeps = [ "libR.so" ];

  unpackPhase = ''
    dpkg-deb -x $src .
  '';

  installPhase = ''
    mkdir -p $out/
    cp -r usr/* $out/

    substituteInPlace $out/share/applications/rstudio.desktop \
      --replace-fail "/usr/lib/rstudio/rstudio" "$out/bin/rstudio"

    # Manually add R lib to rsession's rpath
    patchelf --add-rpath ${pkgs.R}/lib/R/lib $out/lib/rstudio/resources/app/bin/rsession

    # Create R profile to set library paths (rstudioWrapper technique)
    cat > $out/fix_libs.R << EOF
    # Autogenerated to fix R library paths
    .libPaths(c(.libPaths(), "${
      lib.concatMapStringsSep "\", \"" (p: "${p}/library") (rEnv.buildInputs)
    }"))
    EOF

    makeWrapper $out/lib/rstudio/rstudio $out/bin/rstudio \
      --set RSTUDIO_WHICH_R ${rEnv}/bin/R \
      --set R_PROFILE_USER $out/fix_libs.R \
      --prefix XDG_DATA_DIRS : ${pkgs.gsettings-desktop-schemas}/share/gsettings-schemas/${pkgs.gsettings-desktop-schemas.name} \
      --add-flags "--disable-gpu --disable-software-rasterizer"
  '';

  meta = with lib; {
    descripiton = "Rstudio Desktop";
    platforms = platforms.linux;
  };
}
